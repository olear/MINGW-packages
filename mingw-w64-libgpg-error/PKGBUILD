# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libgpg-error
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.20
pkgrel=1
pkgdesc="Support library for libgcrypt (mingw-w64)"
arch=('any')
url="http://www.gnupg.org"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-gettext")
options=('staticlibs' 'strip')
source=("ftp://ftp.gnupg.org/gcrypt/libgpg-error/${_realname}-${pkgver}.tar.bz2"
        01-mingw32-fix-potomo.mingw.patch
        02-fix-symbollist-on.mingw.patch
        04-use-winpthreads.mingw.patch
        05-w32-gen.all.patch
        07-windows-build.patch
        09-fix-generating-union.patch)
md5sums=('9997d9203b672402a04760176811589d'
         '4865ce481ec9aa5efc0a4979f197a686'
         'a69b0fd4c8e4ebda9ddeb68e565bf1b1'
         'f7650decafae57ef791318b6a7e8f7e7'
         'c2a1405908895e98c877201204a410e2'
         '688227799064deb20699cc37fd3cee7d'
         '14cc65006c4c19919182c56759be7b95')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/01-mingw32-fix-potomo.mingw.patch
  patch -p1 -i ${srcdir}/02-fix-symbollist-on.mingw.patch
  #patch -p1 -i ${srcdir}/03-fix-config-on.mingw.patch
  #patch -p1 -i ${srcdir}/04-use-winpthreads.mingw.patch
  patch -p1 -i ${srcdir}/05-w32-gen.all.patch
  patch -p1 -i ${srcdir}/07-windows-build.patch
  #patch -p1 -i ${srcdir}/09-fix-generating-union.patch
  autoreconf -fi
}

build() {
  [ -d ${srcdir}/build-${MINGW_CHOST} ] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-shared \
      --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
