# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libvpx
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.0
pkgrel=1
pkgdesc="The VP8 Codec SDK (mingw-w64)"
arch=('any')
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-yasm")
url="http://www.webmproject.org/"
source=(${_realname}-${pkgver}.tar.gz::https://github.com/webmproject/libvpx/archive/v${pkgver}.tar.gz
        0001-enable-shared-on.mingw.patch
        0002-no-pthreads.mingw.patch
        0003-cross-with-native-binutils.mingw.patch
        0005-fix-exports.mingw.patch
        0006-instll-implib.mingw.patch
        0008-fix-ln-on-install.mingw.patch)
options=('strip' 'staticlibs')
md5sums=('0c662bc7525afe281badb3175140d35c'
         '6fb5ae049d34259ef403f2dfad997974'
         '8f8ae828cbe1803136611d5fa9a04acf'
         '9f66ab98dbbe170ca7dc0c820f56558a'
         'd33bd15169b21f5c6cba379d6c76397f'
         'c91d82639fee7d82cf69b13a34c05179'
         '1a5f3e0e8ff8126fbfd8bb133e7e1c87')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -Np1 -i ${srcdir}/0001-enable-shared-on.mingw.patch
  patch -Np1 -i ${srcdir}/0002-no-pthreads.mingw.patch
  patch -Np1 -i ${srcdir}/0003-cross-with-native-binutils.mingw.patch
  patch -Np1 -i ${srcdir}/0005-fix-exports.mingw.patch
}

build() {
  if [ ${MINGW_CHOST} = "i686-w64-mingw32" ]; then
    _targetarch="x86-win32-gcc"
  else
    _targetarch="x86_64-win64-gcc"
  fi
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --target=${_targetarch} \
    --enable-vp8 \
    --enable-vp9 \
    --enable-runtime-cpu-detect \
    --enable-postproc \
    --enable-pic \
    --enable-shared \
    --enable-static \
    --enable-experimental \
    --enable-spatial-svc \
    --disable-examples \
    --disable-docs \
    --disable-install-docs \
    --disable-install-srcs

  for _ff in *.mk; do
    sed -i "s/HAVE_GNU_STRIP=yes/HAVE_GNU_STRIP=no/g" $_ff
  done
  make #-j1
}

package() {
  cd "${srcdir}/build-$MINGW_CHOST"
  make DIST_DIR="${pkgdir}${MINGW_PREFIX}" install
}
