# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pkg-config
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.29
pkgrel=1
pkgdesc="A system for managing library compile/link flags (mingw-w64)"
arch=('any')
url="http://pkgconfig.freedesktop.org/wiki/"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-libwinpthread")
conflicts=("${MINGW_PACKAGE_PREFIX}-pkgconf")
replaces=("${MINGW_PACKAGE_PREFIX}-pkgconf")
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=(http://pkgconfig.freedesktop.org/releases/${_realname}-${pkgver}.tar.gz
        0010-expand-paths.mingw.patch
        0011-platform-dependent-separator-and-adjustable-prefix.mingw.patch
        1001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
        1003-g_abort.all.patch
        1005-glib-send-log-messages-to-correct-stdout-and-stderr.patch
        1017-glib-use-gnu-print-scanf.patch
        1024-return-actually-written-data-in-printf.all.patch
        1030-fix-stat.all.patch)
md5sums=('77f27dce7ef88d0634d0d6f90e03a77f'
         'cb65caa60584d471aa6f76b181790ee1'
         'd6b6acdb90f93c35378f53e34e7cd6fa'
         '10cf1e482b6a1367d79648466f2cf23c'
         '629431fe82711812531f6367205783e8'
         '80fb538441701d2dd3ab8bcb13baf90c'
         '22105e191551caf9d94d9f637f05ed77'
         '3f896edd80eaa9c4e98abca0df6c83bd'
         '963a72f6efcaa3405204b76b27b44645')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0010-expand-paths.mingw.patch
  patch -p1 -i ${srcdir}/0011-platform-dependent-separator-and-adjustable-prefix.mingw.patch
  
  cd glib

  patch -Np1 -i "${srcdir}/1001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch"
  patch -Np1 -i "${srcdir}/1003-g_abort.all.patch"
  patch -Np1 -i "${srcdir}/1005-glib-send-log-messages-to-correct-stdout-and-stderr.patch"
  patch -Np1 -i "${srcdir}/1017-glib-use-gnu-print-scanf.patch"
  patch -Np1 -i "${srcdir}/1024-return-actually-written-data-in-printf.all.patch"
  patch -Np1 -i "${srcdir}/1030-fix-stat.all.patch"
  
  cd ..
  
  autoreconf -fi
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-internal-glib \
    --disable-indirect-deps \
    --with-default-prefix-for-default-paths=${MINGW_PREFIX}

  make
}

check() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make check
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}
