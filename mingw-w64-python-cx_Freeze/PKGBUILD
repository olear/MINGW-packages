# Maintainer: Frode Solheim <frode@fs-uae.net>

_realname=cx_Freeze
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-cx_Freeze"
         "${MINGW_PACKAGE_PREFIX}-python3-cx_Freeze")
pkgver=4.3.3
pkgrel=5
pkgdesc="Python package for freezing scripts into executables (mingw-w64)"
arch=('any')
license=('PSF')
url="http://cx-freeze.sourceforge.net/"
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3")
options=('staticlibs' 'strip' '!debug')
source=("https://pypi.python.org/packages/source/c/cx_Freeze/cx_Freeze-${pkgver}.tar.gz")
md5sums=('3cae24b98694540eb083ac500c0d4aa1')

prepare() {
  rm -Rf python2-cx_Freeze-${pkgver}-${CARCH}
  cp -a cx_Freeze-${pkgver} python2-cx_Freeze-${pkgver}-${CARCH}
  rm -Rf python3-cx_Freeze-${pkgver}-${CARCH}
  cp -a cx_Freeze-${pkgver} python3-cx_Freeze-${pkgver}-${CARCH}
}

build() {
  echo "Building for Python 2"
  cd python2-cx_Freeze-${pkgver}-${CARCH}
  ${MINGW_PREFIX}/bin/python2 setup.py build
  cd ..

  echo "Building for Python 3"
  cd python3-cx_Freeze-${pkgver}-${CARCH}
  ${MINGW_PREFIX}/bin/python3 setup.py build
  cd ..
}

package_python2-cx_Freeze() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-six")

  cd python2-cx_Freeze-${pkgver}-${CARCH}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
         --root="${pkgdir}" --optimize=1

  mv ${pkgdir}${MINGW_PREFIX}/bin/cxfreeze{,2}
  mv ${pkgdir}${MINGW_PREFIX}/bin/cxfreeze-postinstall{,2}
  mv ${pkgdir}${MINGW_PREFIX}/bin/cxfreeze-quickstart{,2}
}

package_python3-cx_Freeze() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-python3-six")

  cd python3-cx_Freeze-${pkgver}-${CARCH}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1
}

package_mingw-w64-i686-python2-cx_Freeze() {
  package_python2-cx_Freeze
}

package_mingw-w64-i686-python3-cx_Freeze() {
  package_python3-cx_Freeze
}

package_mingw-w64-x86_64-python2-cx_Freeze() {
  package_python2-cx_Freeze
}

package_mingw-w64-x86_64-python3-cx_Freeze() {
  package_python3-cx_Freeze
}
