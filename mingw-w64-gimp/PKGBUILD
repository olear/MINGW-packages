# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=gimp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.8.16
pkgrel=1
pkgdesc="GNU Image Manipulation Program (mingw-w64)"
arch=('any')
url="http://www.gimp.org/"
license=('GPL' 'LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-babl"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-dbus-glib"
         "${MINGW_PACKAGE_PREFIX}-gegl"
         "${MINGW_PACKAGE_PREFIX}-ghostscript"
         "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-jasper"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libexif"
         "${MINGW_PACKAGE_PREFIX}-libmng"
         "${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-libwmf"
         "${MINGW_PACKAGE_PREFIX}-openexr"
         "${MINGW_PACKAGE_PREFIX}-poppler"
         "${MINGW_PACKAGE_PREFIX}-python2-pygtk"
         "${MINGW_PACKAGE_PREFIX}-python2-gobject"
         "${MINGW_PACKAGE_PREFIX}-webkitgtk2"
         "${MINGW_PACKAGE_PREFIX}-xpm-nox"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-iso-codes"
             "intltool")
options=('strip' 'makeflags')
install=gimp-${CARCH}.install
source=(http://download.gimp.org/pub/gimp/v${pkgver%.*}/${_realname}-${pkgver}.tar.bz2
        gegl-0.3-fix.patch
        find-directx-fix.patch
        python-m4.patch
        replace-pygtk-codegen.patch)
md5sums=('30e0a1b7c18b0e3415f4ac54567252ac'
         'd1e19159164cac7b39578a6b0bb02cbb'
         '0df0c9c5f55012023894ed8b959c2fa9'
         '460f010cfb789608d2045a4a936f47b2'
         '99c9edb5613db63033e043261e53d2c3')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/gegl-0.3-fix.patch
  patch -p1 -i ${srcdir}/find-directx-fix.patch
  patch -p1 -i ${srcdir}/python-m4.patch
  patch -p1 -i ${srcdir}/replace-pygtk-codegen.patch

  rm -fr autom4te.cache
  libtoolize -c
  aclocal -I m4macros
  automake -a -c
  autoheader
  autoconf

  #autoreconf -fi -I m4macros
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  PYTHON=${MINGW_PREFIX}/bin/python2 \
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --enable-mp \
    --enable-gimp-console \
    --enable-python \
    --with-gif-compression=lzw \
    --with-libcurl \
    --without-aa \
    --with-lcms=lcms2 \
    --with-pdbgen \
    --with-directx-sdk=${MINGW_PREFIX}/${MINGW_CHOST} \
    --without-x

  # V=1 because of a race condition (a generated 'bin2c' type
  # file ending up truncated once). This might avoid it.
  make V=1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  ln -s gimptool-2.0 "${pkgdir}${MINGW_PREFIX}/bin/gimptool.exe"
  cp ${pkgdir}${MINGW_PREFIX}/lib/gimp/2.0/{plug-ins,python}/pyconsole.py
  sed -e "s|${MINGW_PREFIX}/bin/python2|python2w.exe|g" -i ${pkgdir}${MINGW_PREFIX}/lib/gimp/2.0/interpreters/pygimp.interp
  rm -f "${pkgdir}${MINGW_PREFIX}/lib/*.def"
  rm -f "${pkgdir}${MINGW_PREFIX}/lib/gimp/2.0/modules/*.dll.a"
}
