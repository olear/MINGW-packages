# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=meld
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}3"
pkgver=3.15.1
pkgrel=1
pkgdesc="Visual diff and merge tool (mingw-w64)"
arch=('any')
url="http://meldmerge.org/"
license=("GPL" "LGPL")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-gtksourceview3"
         "${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-python2-gobject")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             #"${MINGW_PACKAGE_PREFIX}-python2-cx_freeze"
             'intltool'
             'itstool')
install=meld-${CARCH}.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        '0001-fixes-for-MinGW-w64-symlinks-and-use-sh-to-spawn.patch'
        "0002-build-exe.patch")
md5sums=('238c3eba59290d5128675c88c1849e05'
         'c3272464b68c2118f19be3f4290034c1'
         'c4770167f70e061c1f94d8c3fa3e7150')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-fixes-for-MinGW-w64-symlinks-and-use-sh-to-spawn.patch"
  #patch -p1 -i "${srcdir}/0002-build-exe.patch"
  sed -i 's/env python/env python2/' bin/meld
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PYTHON=${MINGW_PREFIX}/bin/python2 DISTUTILS_DEBUG=1 ${MINGW_PREFIX}/bin/python2 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py --no-update-icon-cache --no-compile-schemas \
    install --prefix=${MINGW_PREFIX} --root="${pkgdir}"
  PREFIX_WIN=$(cygpath -m ${MINGW_PREFIX})
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/bin/meld
}
