# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Xin Sun <sun.simpson@gmail.com>

DLAGENTS=("https::/usr/bin/curl -k -o %o %u")
_realname=pyside
pkgname=(${MINGW_PACKAGE_PREFIX}-${_realname}-common ${MINGW_PACKAGE_PREFIX}-python2-${_realname})
pkgver=1.2.2
pkgrel=1
arch=('any')
url="http://pyside.github.io/docs/pyside/"
license=("LGPL")
install=${_realname}-${CARCH}.install
source=(https://pypi.python.org/packages/source/P/PySide/PySide-${pkgver}.tar.gz
        pyside.patch)
md5sums=('c45bc400c8a86d6b35f34c29e379e44d'
         'c46448270f258c2fdc1d5d554fc67b18')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-python2-shiboken"
             "${MINGW_PACKAGE_PREFIX}-qt4")
options=('staticlibs' 'strip')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/pyside.patch
}

build() {
  for pyv in python2; do
    local _conf=
    if [ "${pyv}" = "python2" ]; then
      _conf="-DPYTHON_SUFFIX=-python2.7"
    fi
    [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
    cp -rf PySide-${pkgver} build-${MINGW_CHOST}-${pyv}
    pushd ${srcdir}/build-${MINGW_CHOST}-${pyv}/sources/pyside > /dev/null
    
    export MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX"
    ${MINGW_PREFIX}/bin/cmake \
      -G"MSYS Makefiles" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=RELEASE \
	  -DBUILD_TESTS=FALSE \
      ${_conf} \
      -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/${pyv}.exe

    make
    
    popd > /dev/null
  done

}

fix_paths() {
  local prefixwin=$(cygpath -am ${MINGW_PREFIX})
  local prefixinst=$(cygpath -am ${pkgdir}${MINGW_PREFIX})
  
  local _files=$(find ${pkgdir}${MINGW_PREFIX}/lib/cmake/PySide-${pkgver} -type f -iname "*.cmake" -o -iname "*.pc")
  for f in ${_files}; do
    plain "Patching ${f}..."
    sed -e "s|${prefixwin}|${MINGW_PREFIX}|g" \
        -e "s|${prefixinst}|${MINGW_PREFIX}|g" \
        -i ${f}
  done
}

package_pyside-common() {
  pkgdesc="Provides LGPL Qt bindings for Python and related tools for binding generation (Common files)(mingw-w64)"

  
  
  fix_paths
}

package_python2-pyside() {
  pkgdesc="Provides LGPL Qt bindings for Python and related tools for binding generation (Python2)(mingw-w64)"
  makedepends=("${MINGW_PACKAGE_PREFIX}-pyside-common"
               "${MINGW_PACKAGE_PREFIX}-python2"
               "${MINGW_PACKAGE_PREFIX}-python2-shiboken"
               "${MINGW_PACKAGE_PREFIX}-qt4")

  cd ${srcdir}/build-${MINGW_CHOST}-python2/sources/pyside
  make DESTDIR=${pkgdir} install
  
  #install -Dm644 PySideConfig-python2.7.cmake ${pkgdir}${MINGW_PREFIX}/lib/cmake/PySide-${pkgver}/
  mv ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/pyside.pc ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/pyside-py2.pc
  sed -i 's/shiboken/shiboken-py2/g' ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/pyside-py2.pc
  
  #rm -rf ${pkgdir}${MINGW_PREFIX}/{include,share}
  #rm ${pkgdir}${MINGW_PREFIX}/lib/cmake/PySide-${pkgver}/PySideConfigVersion.cmake
  #rm ${pkgdir}${MINGW_PREFIX}/lib/cmake/PySide-${pkgver}/PySideConfig.cmake
  
  fix_paths
}

# Wrappers
package_mingw-w64-i686-pyside-common() {
  package_pyside-common
}

package_mingw-w64-i686-python2-pyside() {
  package_python2-pyside
}


package_mingw-w64-x86_64-pyside-common() {
  package_pyside-common
}

package_mingw-w64-x86_64-python2-pyside() {
  package_python2-pyside
}

