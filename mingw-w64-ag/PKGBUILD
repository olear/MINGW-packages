# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=ag
_longname=the_silver_searcher
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.31.0.r1666.0e577cc
pkgrel=1
pkgdesc="The Silver Searcher: An attempt to make something better than ack, which itself is better than grep (mingw-w64)"
arch=('any')
url="http://geoff.greer.fm/ag"
license=("Apache")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "git")
depends=("${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs') # '!strip' 'debug')
source=("${_realname}"::"git+https://github.com/ggreer/the_silver_searcher.git"
        "0001-lang-Allow-both-extensions-and-full-filenames.patch"
        "0002-options-Fix-ordering-problems-with-color.patch"
        "0003-lang-Add-autotools-ac-am-in-m4-pc.patch"
        "0004-lang-Add-to-make-Makefile-Makefile.Debug-Makefile.Re.patch"
        "0005-lang-Add-makepkg-PKGBUILD-diff-patch-in-install.patch"
        "0006-lang-Add-cmake-CMakeLists.txt-CMakeCache.txt.patch")
md5sums=('SKIP'
         '73e60af8e8f0eb12e69797a6377b3ba9'
         '8364f2765d95d3a550cd7698c3540ab6'
         '6dc9b48f11616f2392935d3297bdabbc'
         'e40bea74e7b32da3543d635e1984be64'
         '40ef6ecb9db4c7b8ae6e8a13b5ecafb8'
         '75e55c3d88d435994e8562d29ada825e')

pkgver() {
  cd ${srcdir}/${_realname}
  local AC_INIT_VER=$(grep AC_INIT configure.ac -A5 | tr '\n' ' ' | sed -e 's/AC_INIT([^,]\+,\s*\[\?\([0-9.a-z]\+\).*/\1/')
  printf "%s.r%s.%s" "${AC_INIT_VER}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/${_realname}
  git am "${srcdir}"/0001-lang-Allow-both-extensions-and-full-filenames.patch
  git am "${srcdir}"/0002-options-Fix-ordering-problems-with-color.patch
  git am "${srcdir}"/0003-lang-Add-autotools-ac-am-in-m4-pc.patch
  git am "${srcdir}"/0004-lang-Add-to-make-Makefile-Makefile.Debug-Makefile.Re.patch
  git am "${srcdir}"/0005-lang-Add-makepkg-PKGBUILD-diff-patch-in-install.patch
  git am "${srcdir}"/0006-lang-Add-cmake-CMakeLists.txt-CMakeCache.txt.patch

  # configure.ac forces -O2, so force it to -O0 if debugging.
  if check_option "debug" "y"; then
    sed -i "s#-O2#-O0#g" configure.ac
  fi

  autoreconf -fi
}

build() {

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  "${srcdir}"/${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --{build,host}=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
