# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

# gtk3 support is still experimental
# It can be tested via _use_gtk3=yes
_use_gtk3=no
if [ "${_use_gtk3}" = "yes" ]; then
  _gtksuff=3
fi

_realname=inkscape
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.91
pkgrel=11
pkgdesc="Vector graphics editor using the SVG file format (mingw-w64)"
arch=('any')
url="http://inkscape.sourceforge.net/"
license=("GPL" "LGPL")
install=inkscape-${CARCH}.install
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "intltool")
depends=(
          "${MINGW_PACKAGE_PREFIX}-aspell"
          "${MINGW_PACKAGE_PREFIX}-gc"
          "${MINGW_PACKAGE_PREFIX}-ghostscript"
          "${MINGW_PACKAGE_PREFIX}-gsl"
          "${MINGW_PACKAGE_PREFIX}-gtkmm${_gtksuff}"
          "${MINGW_PACKAGE_PREFIX}-gtkspell${_gtksuff}"
          "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
          "${MINGW_PACKAGE_PREFIX}-imagemagick"
          "${MINGW_PACKAGE_PREFIX}-lcms2"
          "${MINGW_PACKAGE_PREFIX}-libcdr"
          "${MINGW_PACKAGE_PREFIX}-libxml2"
          "${MINGW_PACKAGE_PREFIX}-libxslt"
          "${MINGW_PACKAGE_PREFIX}-libwpg"
          "${MINGW_PACKAGE_PREFIX}-poppler"
          "${MINGW_PACKAGE_PREFIX}-popt"
          "${MINGW_PACKAGE_PREFIX}-python2"
          $([[ "${_use_gtk3}" == "yes" ]] && echo \
            "${MINGW_PACKAGE_PREFIX}-gdl" ) \
        )
optdepends=(#"${MINGW_PACKAGE_PREFIX}-pstoedit: latex formulas"
            #"${MINGW_PACKAGE_PREFIX}-texlive-core: latex formulas"
            "${MINGW_PACKAGE_PREFIX}-python2-numpy: some extensions"
            "${MINGW_PACKAGE_PREFIX}-python2-lxml: some extensions and filters"
            #"${MINGW_PACKAGE_PREFIX}-uniconvertor: reading/writing to some proprietary formats"
            )
options=('staticlibs' 'strip')
source=("https://inkscape.global.ssl.fastly.net/media/resources/file/${_realname}-${pkgver}.tar.gz"
        inkscape-0.48.4-headers-fix.patch
        inkscape-0.48.4-python.patch
        inkscape-0.48.4-use-GStatBuf-insted-stat.patch
        inkscape-0.48.4-use-GStatBuf-insted-stat-gtk3.patch
        inkscape-0.48.4-poppler-includes-conflicts.patch
        inkscape-0.48.4-win32-relocatable.patch
        inkscape-0.48.4-win32-file-dialog.patch
        inkscape-0.48.4-python-extensions-location.patch
        inkscape-0.91-simulaneous-intltool-gettext.patch)
sha1sums=('e590f3b52c10771ae776a6f8173d21602ad1f261'
          '12fddb0a0b34b4308de1979d13b26d05a6ec8f77'
          '4dd0da65e538ba7e73a42ce76efd5e5a155d3bf0'
          '7e53e26506f22e51b79c50302adcad693c2d1698'
          '1ccdc6730fadca589e509fda497b351e80b7238a'
          '8d65767ed2b291ccae70d8718f0b7a764632c445'
          '6c673021dd67e42a5288e14d53275794e3b7417f'
          'f9daedc7c77b4d4a2670044e634594265f3b1926'
          '185cd8f6e4ad0b55862720c47f80da9dfa8a82ff'
          '5e0d67e5548501e20f7ed68a1c57b1789dc1bff3')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/inkscape-0.48.4-headers-fix.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-python.patch
  if [ "${_use_gtk3}" = "no" ]; then
    patch -p1 -i ${srcdir}/inkscape-0.48.4-use-GStatBuf-insted-stat.patch
  else
    patch -p1 -i ${srcdir}/inkscape-0.48.4-use-GStatBuf-insted-stat-gtk3.patch
  fi
  patch -p1 -i ${srcdir}/inkscape-0.48.4-poppler-includes-conflicts.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-win32-relocatable.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-win32-file-dialog.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-python-extensions-location.patch
  patch -p1 -i ${srcdir}/inkscape-0.91-simulaneous-intltool-gettext.patch

  sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
  sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
  #sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
  sed -i 's|python -c|python2 -c|g' configure share/extensions/uniconv*.py
  sed -i '/^#include <g.kmm/i #include <glibmm.h>' src/*{,/*{,/*{,/*}}}.{h,cpp}
  sed -e "s|export-dynamic|export-all-symbols|g" -i src/Makefile.am

  autoreconf -fi -I m4
}

build() {
  # export lt_cv_deplibs_check_method='pass_all'
  [[ -d build-${CARCH} ]] && rm -rf build-${CARCH}
  mkdir -p build-${CARCH} && cd build-${CARCH}

  declare -a extra_config
  if [ "${_use_gtk3}" = "yes" ]; then
    extra_config+=(--enable-gtk3-experimental)
  fi

  CXXFLAGS+=" -std=gnu++11"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-lcms \
    --enable-poppler-cairo \
    --disable-dependency-tracking \
    ${extra_config[@]}

  make
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
