# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=aspell-ru
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pkgver=0.99f7-1
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Russian dictionary for aspell (mingw-w64)"
arch=('any')
url="http://aspell.net/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-aspell")
source=(ftp://ftp.gnu.org/gnu/aspell/dict/ru/aspell6-ru-${_pkgver}.tar.bz2
        001-unixy-dirs.patch)
md5sums=('c4c98eaa5e77ad3adccbc5c96cb57cb3'
         '59759a5f0047312a49f5817a83a8a2f9')
sha1sums=('e012fa03645f4ff1f5ba9df6b215ea4ffc6fd9cf'
          'a624663ef98117b62f45cab4ade44cce3105ef4d')

prepare() {
  cd "${srcdir}/aspell6-ru-${_pkgver}"
  patch -p1 -i ${srcdir}/001-unixy-dirs.patch
}

build() {
  cd "${srcdir}/aspell6-ru-${_pkgver}"
  ./configure
  sed -i 's/C\:\\msys64\\/\//' Makefile
  make
}

package() {
  cd "${srcdir}/aspell6-ru-${_pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 Copyright "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
