# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=glpk
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.57
pkgrel=1
pkgdesc="GNU Linear Programming Kit: solve LP, MIP and other problems (mingw-w64)"
arch=('any')
url="http://www.gnu.org/software/glpk/glpk.html"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-gmp")
options=('staticlibs' !strip debug)
source=("http://ftp.gnu.org/gnu/glpk/glpk-${pkgver}.tar.gz"
        'timeval-64bit.patch')
sha1sums=('d2df2de59798c35024df8d140a53453e29a095be'
          'b496de484eb3df16dd91c253cd8230b4e42fb902')

prepare () {
  cd "${srcdir}/glpk-${pkgver}"
  patch -p1 -i "${srcdir}/timeval-64bit.patch"
  sed -i "s|-version-info|-no-undefined -version-info|g" src/Makefile.am
  autoreconf -vfi
}

build() {
  [[ -d build-${CARCH} ]] && rm -rf build-${CARCH}
  mkdir -p build-${CARCH} && cd build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --with-gmp \
    --with-gnu-ld

  make
}

check() {
  cd "${srcdir}/build-${CARCH}"
  make check
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
