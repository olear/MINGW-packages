# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=sphinx-alabaster-theme
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.7.6
pkgrel=1
pkgdesc="Modified Kr Sphinx doc theme (mingw-w64)"
arch=('any')
license=('BSD')
url='https://github.com/bitprophet/alabaster'
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
options=('staticlibs')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/bitprophet/alabaster/archive/${pkgver}.tar.gz")
md5sums=('940d47184c4d67d9e1943ba2e5b15a65')

prepare() {
  cd ${srcdir}
  cp -r alabaster-${pkgver} python2-build
  cp -r alabaster-${pkgver} python3-build
}

build() {
  cd "${srcdir}"
  for builddir in python{2,3}-build; do
    pushd ${builddir}
    ${MINGW_PREFIX}/bin/${builddir%-build} setup.py build
    popd
  done
}

check() {
  cd "${srcdir}"
  for builddir in python{2,3}-build; do
    pushd ${builddir}
    ${MINGW_PREFIX}/bin/${builddir%-build} py.test
    popd
  done
}

package_python2-sphinx-alabaster-theme() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_python3-sphinx-alabaster-theme() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-sphinx-alabaster-theme() {
  package_python2-sphinx-alabaster-theme
}

package_mingw-w64-i686-python3-sphinx-alabaster-theme() {
  package_python3-sphinx-alabaster-theme
}

package_mingw-w64-x86_64-python2-sphinx-alabaster-theme() {
  package_python2-sphinx-alabaster-theme
}

package_mingw-w64-x86_64-python3-sphinx-alabaster-theme() {
  package_python3-sphinx-alabaster-theme
}
