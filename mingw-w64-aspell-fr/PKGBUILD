# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=aspell-fr
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.50
pkgrel=1
pkgdesc="French dictionary for aspell (mingw-w64)"
arch=('any')
url="http://aspell.net/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-aspell")
source=(ftp://ftp.gnu.org/gnu/aspell/dict/fr/aspell-fr-${pkgver}-3.tar.bz2
        001-unixy-dirs.patch)
md5sums=('53a2d05c4e8f7fabd3cefe24db977be7'
         '2bdec16a2dc204a49378609f90fb436f')
sha1sums=('4712f81069eb20763aaf855f73b2819f4805f132'
          'f386c73e7bb57563850e980d723f2711608fd7ea')

prepare() {
  cd "${srcdir}/aspell-fr-${pkgver}-3"
  patch -p1 -i ${srcdir}/001-unixy-dirs.patch
}

build() {
  cd "${srcdir}/aspell-fr-${pkgver}-3"
  ./configure
  sed -i 's/C\:\\msys64\\/\//' Makefile
  make
}

package() {
  cd "${srcdir}/aspell-fr-${pkgver}-3"
  make DESTDIR="${pkgdir}" install

  install -D -m644 Copyright "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
