# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gnupg
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.1.9
pkgrel=1
pkgdesc="GNU Privacy Guard - a PGP replacement tool (mingw-w64)"
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-libksba"
         "${MINGW_PACKAGE_PREFIX}-libgcrypt"
         "${MINGW_PACKAGE_PREFIX}-libassuan"
         "${MINGW_PACKAGE_PREFIX}-libsystre"
         "${MINGW_PACKAGE_PREFIX}-libusb-compat"
         "${MINGW_PACKAGE_PREFIX}-npth"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("texinfo" "${MINGW_PACKAGE_PREFIX}-gcc")
# REMOVE openldap package before build
license=('GPL')
options=(emptydirs)
url="http://www.gnupg.org/"
source=(ftp://ftp.gnupg.org/gcrypt/gnupg/${_realname}-${pkgver}.tar.bz2{,.sig}
        01-mingw32-fix-potomo.mingw.patch
        02-mingw-sysconfigdir-under-prefix.patch
        03-ldap-libraries-check-order.patch
        04-dos-extra-defs.patch)
sha1sums=('119bab38d2ff3a849be62914be9bf7333da68883'
          'SKIP'
          'adca40b456c054db3aae5f112e18f56235322a45'
          'e55b64ef1b1d9e0a224c2b8716d7a490aed6070d'
          '18634884e51f93b62a6c8b75233d175ec455f033'
          'afe4c750932c122d7dc182e7a136147f63d1c211')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/01-mingw32-fix-potomo.mingw.patch
  patch -p1 -i ${srcdir}/02-mingw-sysconfigdir-under-prefix.patch
  patch -p1 -i ${srcdir}/03-ldap-libraries-check-order.patch
  patch -p1 -i ${srcdir}/04-dos-extra-defs.patch
  
  autoreconf -fiv
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --libexecdir=${MINGW_PREFIX}/bin \
      --sbindir=${MINGW_PREFIX}/bin \
      --sysconfdir=${MINGW_PREFIX}/etc/gnupg \
      --enable-maintainer-mode \
      --disable-symcryptrun \
      --enable-gpgtar \
      --disable-g13
  MSYS2_ARG_CONF_EXCL="-DGNUPG_BINDIR=;-DGNUPG_LIBEXECDIR=;-DGNUPG_LIBDIR=;-DGNUPG_DATADIR=;-DGNUPG_SYSCONFDIR=;-DGNUPG_LOCALSTATEDIR=;-DLOCALEDIR=" \
  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  MSYS2_ARG_CONF_EXCL="-DGNUPG_BINDIR=;-DGNUPG_LIBEXECDIR=;-DGNUPG_LIBDIR=;-DGNUPG_DATADIR=;-DGNUPG_SYSCONFDIR=;-DGNUPG_LOCALSTATEDIR=;-DLOCALEDIR=" \
  make -j1 DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/{etc/gnupg,var/cache/gnupg,home}
}
