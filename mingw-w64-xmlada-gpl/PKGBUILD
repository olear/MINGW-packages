# Maintainer: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

# Pathnames in this project can get quite long, so at least on Windows
# I recommend to use a short BUILDDIR setting to avoid problems
#

_realname=xmlada-gpl
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgrel=1
pkgver=2014
pkgdesc="A full XML suite for Ada"
arch=('any')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname%-*}")
url="http://libre.adacore.com/libre/tools/xmlada/"
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-ada")
source=(http://downloads.dragonlace.net/src/${_realname}-${pkgver}-src.tar.gz)
md5sums=('98c96b8c6a877617ec4da3ef6a03288a')
options=('strip')

build() {
  cd ${srcdir}/${_realname:0:6}-${pkgver}-src
  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-shared
  make
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}/include/xmlada
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/xmlada/static
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/xmlada/relocatable
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/gnat/xmlada

  cd ${srcdir}/${_realname:0:6}-${pkgver}-src
  make prefix=${pkgdir}${MINGW_PREFIX} INSTALL=cp install

  rm -rf ${pkgdir}${MINGW_PREFIX}/share/examples
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/doc

  # Copy License Files
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname
  cp -pf ${srcdir}/${_realname:0:6}-${pkgver}-src/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
}
