# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=aspell
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_date_ver=20131207
_ver_base=0.60.7
pkgver=${_ver_base}.${_date_ver}
pkgrel=2
pkgdesc="A spell checker designed to eventually replace Ispell (mingw-w64)"
arch=('any')
url="http://aspell.net/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-gettext")
options=('strip' 'staticlibs')
source=("ftp://alpha.gnu.org/gnu/${_realname}/${_realname}-${_ver_base}-${_date_ver}.tar.gz"
        0001-use-namespace.mingw.patch
        0002-printf.mingw.patch
        0003-no-undefined-on.mingw.patch
        0004-reloc.mingw.patch
        0005-w32-home.all.patch
        0006-abort.mingw.patch
        0007-fix-including-langinfo.patch)
md5sums=('fd87c9cceb4cc8aff06822c109b9dca3'
         'ee6e262260e11bc285ce6a242fec3aeb'
         '9f799391cc00cf9e0d5ffee88d103a97'
         '4aa79ac556d2cd42b1d736e031acdbac'
         '102bacf0b0c041f9d04aeb6bb0adb865'
         '3830ee14ff5b704a4e21b2c76f153217'
         '5ed7ead83958a39719242221c3669311'
         '50a79bb773b73cb3a69ec38cb086ca27')

prepare() {
  cd "${srcdir}"/${_realname}-${_ver_base}-${_date_ver}
  patch -p1 -i ${srcdir}/0001-use-namespace.mingw.patch
  patch -p1 -i ${srcdir}/0002-printf.mingw.patch
  patch -p1 -i ${srcdir}/0003-no-undefined-on.mingw.patch
  patch -p1 -i ${srcdir}/0004-reloc.mingw.patch
  patch -p1 -i ${srcdir}/0005-w32-home.all.patch
  patch -p1 -i ${srcdir}/0006-abort.mingw.patch
  patch -p1 -i ${srcdir}/0007-fix-including-langinfo.patch

  autoreconf -fi
}

build() {
  CPPFLAGS+=" -DENABLE_W32_PREFIX=1"
  mv ${_realname}-${_ver_base}-${_date_ver} build-${MINGW_CHOST}
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --disable-curses

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install
}
