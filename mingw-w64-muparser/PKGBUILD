# Maintainer: RÃ©mi BERTHO <remi.bertho@openmailbox.org>

_realname=muparser
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.5
pkgrel=1
pkgdesc="A fast math parser library (mingw-w64)"
arch=('any')
url="http://muparser.beltoforion.de/"
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
options=('!makeflags')
source=("${_realname}-${pkgver}.tar.gz"::https://github.com/beltoforion/muparser/archive/v${pkgver}.tar.gz
        "CMakeLists.txt")
sha1sums=('d86946f8bc5f5d74063313a00c16798a418e308b'
          '76f802239c1a5e0bff713db99ce60a9956a20257')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  cp "${srcdir}/CMakeLists.txt" .
}

build() {
  rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  # license
  install -D -m644 ${srcdir}/${_realname}-${pkgver}/License.txt \
  ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

